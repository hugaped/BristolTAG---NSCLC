#Fixed effects 1st order fractional polynomial model (e.g. Weibull (P1=0) and Gompertz (P1=1))
model{                         	  # *** PROGRAM STARTS
for (j in 1:N){   							   # LOOP THROUGH EVENTS

	# time in months transformed according to power P1 
	  timen[j] <- (time[j])    
	  timen1[j] <- (equals(P1,0)*log(timen[j])+(1-equals(P1,0))*pow(timen[j],P1) ) 

	  r[j] ~ dbin(p[j], z[j])      		  # likelihood according to eq. 
	  p[j] <- 1-exp(-h[j]*dt[j])   		  # hazard rate in each interval standardized by unit of time

	#Fixed effects model
	# hazard over time according to FP
	  log(h[j]) <- Alpha[s[j],a[j],pdl[j],1] + Alpha[s[j],a[j],pdl[j],2] * timen1[j] 
	  
	#Deviance contribution
	rhat[j] <- p[j] * z[j]  # expected value of the numerators
	dev[j] <- (2*(r[j]*(log(r[j])-log(rhat[j])) + (z[j]-r[j])*(log(z[j]-r[j]) - log(z[j]-	rhat[j]))))
 }

for (i in 1:ns.pdl){                  # LOOP THROUGH STUDIES REPORTING DISTINCT PDL1 CATEGORIES
  for (k in 1:na[i]){             # LOOP THROUGH ARMS
	for (m in 1:npdl) {				# LOOP THROUGH PDL1 CATEGORIES
		#Alpha[i,k,m,1] <- mu[i,m,1] + d[t[i,k],1] - d[t[i,1],1]  # model for linear predictor of alpha_0 
		#Alpha[i,k,m,2] <- mu[i,m,2] + d[t[i,k],2] - d[t[i,1],2] # model for linear predictor of alpha_1
		
		Alpha[i,k,m,1] <- mu[i,m,1] + d[t[i,k],m,1] - d[t[i,1],m,1]  # model for linear predictor of alpha_0 
		Alpha[i,k,m,2] <- mu[i,m,2] + d[t[i,k],m,2] - d[t[i,1],m,2] # model for linear predictor of alpha_1
	}
   }
 }
 
 for (i in (ns.pdl+1):ns){                  # LOOP THROUGH STUDIES REPORTING MIXED PDL1 (>1%)
  for (k in 1:na[i]){             # LOOP THROUGH ARMS
  
	Alpha[i,k,1,1] <- mu[i,1,1] + (d[t[i,k],1,1] - d[t[i,1],1,1]) * p10pct[i] + (d[t[i,k],2,1] - d[t[i,1],2,1]) * (1-p10pct[i]) # model for linear predictor of alpha_0 
    Alpha[i,k,1,2] <- mu[i,1,2] + (d[t[i,k],1,2] - d[t[i,1],1,2]) * p10pct[i] + (d[t[i,k],2,2] - d[t[i,1],2,2]) * (1-p10pct[i]) # model for linear predictor of alpha_1
  
	# HASH OUT IF NOT INCLUDING PDL1<1%
    Alpha[i,k,3,1] <- mu[i,3,1] + (d[t[i,k],3,1] - d[t[i,1],3,1]) # model for linear predictor of alpha_0 for PDL1 <1%
    Alpha[i,k,3,2] <- mu[i,3,2] + (d[t[i,k],3,2] - d[t[i,1],3,2]) # model for linear predictor of alpha_1 for PDL1 <1%
   }
 }
 
 totresdev <- sum(dev[])           #Total Residual Deviance

     
#priors
for (i in 1:ns){        		      # LOOP THROUGH STUDIES
	for (m in 1:npdl) {				# LOOP THROUGH PDL1 CATEGORIES
		mu[i,m,1:2] ~ dmnorm(mean[1:2], prec[1:2,1:2])   # vague priors for all trial baselines 
	}
 }
 
for (m in 1:npdl) {				# LOOP THROUGH PDL1 CATEGORIES
	d[1,m,1]<-0        				         # alpha_0 treatment effect is zero for reference treatment
	d[1,m,2]<-0       					        # alpha_1 treatment effect is zero for reference treatment
	
	for (k in 2:nt){			            # LOOP THROUGH TREATMENTS
		d[k,m,1:2] ~ dmnorm(mean[1:2],prec[1:2,1:2])  # vague priors for treatment effects 
	}
}

}                                 # *** PROGRAM ENDS
