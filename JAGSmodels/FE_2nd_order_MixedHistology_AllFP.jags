
model{

#Fixed effects 1st order fractional polynomial model (e.g. Weibull (P1=0) and Gompertz (P1=1))
for (j in 1:N){   							   # LOOP THROUGH EVENTS

	# time in months transformed according to power P1 
	timen1[j] <- (equals(P1,0) * log(time[j]) + (1-equals(P1,0)) * pow(time[j],P1)   ) 
	timen2[j] <- (  (1-equals(P2,P1)) * (   equals(P2,0) * log(time[j]) + (1-equals(P2,0)) * pow(time[j],P2)  ) + equals(P2,P1) * (   equals(P2,0) * log(time[j])*log(time[j])   +   (1-equals(P2,0)) * pow(time[j],P2) * log(time[j]) ) )

	r[j] ~ dbin(p[j], z[j])      		  # likelihood according to eq. 
	p[j] <- 1-exp(-h[j]*dt[j])   		  # hazard rate standardized by unit of time

	#Fixed effects model
	# hazard over time according to FP
	log(h[j]) <- Alpha[s[j],a[j],1] + Alpha[s[j],a[j],2] * timen1[j] + Alpha[s[j],a[j],3] * timen2[j] 
	  
	#Deviance contribution
	rhat[j] <- p[j] * z[j]  # expected value of the numerators
	dev[j] <- (2*(r[j]*(log(r[j])-log(rhat[j])) + (z[j]-r[j])*(log(z[j]-r[j]) - log(z[j]-	rhat[j]))))
}

for (i in 1:ns){                  # LOOP THROUGH STUDIES
  for (k in 1:na[i]){             # LOOP THROUGH ARMS
	
	# d[,1] = treatment effect FP intercept for Non-Squamous
	# d[,2] = treatment effect on 1st FP coefficient for Non-Squamous
	# d[,3] = treatment effect on 2nd FP coefficient for Non-squamous
	# d[,4] = treatment effect FP intercept for Squamous
	# d[,5] = treatment effect on 1st FP coefficient for Squamous
	# d[,6] = treatment effect on 2nd FP coefficient for squamous
	Alpha[i,k,1] <- mu[i,1] + ((d[t[i,k],1] - d[t[i,1],1]) * (1 - phist[i])) + (phist[i]* (d[t[i,k],4] - d[t[i,1],4]))
	Alpha[i,k,2] <- mu[i,2] + ((d[t[i,k],2] - d[t[i,1],2]) * (1 - phist[i])) + (phist[i]* (d[t[i,k],5] - d[t[i,1],5])) # model for linear predictor of alpha_1
	Alpha[i,k,3] <- mu[i,3] + ((d[t[i,k],3] - d[t[i,1],3]) * (1 - phist[i])) + (phist[i]* (d[t[i,k],6] - d[t[i,1],6])) # model for linear predictor of alpha_2
   }
 }
 
 totresdev <- sum(dev[])           #Total Residual Deviance

#priors
for (i in 1:ns){        		      # LOOP THROUGH STUDIES
  mu[i,1:3] ~ dmnorm(mean[1:3], prec[1:3,1:3])   # vague priors for all trial baselines 
 }
 
d[1,1]<-0        				         	# effect for NS on alpha_0 is zero for reference treatment
d[1,2]<-0       					        # effect for NS on alpha_1 is zero for reference treatment
d[1,3]<-0       					        # effect for NS on alpha_2 is zero for reference treatment
d[1,4]<-0       					        # effect for S on alpha_0 is zero for reference treatment
d[1,5]<-0       					        # effect for S on alpha_1 is zero for reference treatment
d[1,6]<-0       					        # effect for S on alpha_2 is zero for reference treatment

for (k in 2:nt){			            # LOOP THROUGH TREATMENTS
  d[k,1:3] ~ dmnorm(mean[1:3],prec[1:3,1:3])  # vague priors for treatment effects 
  d[k,4] <- d[k,1] + hist.LHR[1]
  d[k,5] <- d[k,2] + hist.LHR[2]
  d[k,6] <- d[k,3] + hist.LHR[3]
 }
 
for (k in 1:3) {
	hist.LHR[k] ~ dnorm(0,0.0001) # Prior for ratio on each FP parameter between S and NS
}

}                                 # *** PROGRAM ENDS


