
model{

#Fixed effects 1st order fractional polynomial model (e.g. Weibull (P1=0) and Gompertz (P1=1))
for (j in 1:N){   							   # LOOP THROUGH EVENTS

	# time in months transformed according to power P1 
	  #timen[j] <- (time[j])    
	  timen1[j] <- (equals(P1,0)*log(time[j])+(1-equals(P1,0))*pow(time[j],P1) ) 

	  r[j] ~ dbin(p[j], z[j])      		  # likelihood according to eq. 
	  p[j] <- 1-exp(-h[j]*dt[j])   		  # hazard rate in each interval standardized by unit of time

	#Fixed effects model
	# hazard over time according to FP
	  log(h[j]) <- Alpha[s[j],a[j],1] + Alpha[s[j],a[j],2] * timen1[j] 
	  
	#Deviance contribution
	rhat[j] <- p[j] * z[j]  # expected value of the numerators
	dev[j] <- (2*(r[j]*(log(r[j])-log(rhat[j])) + (z[j]-r[j])*(log(z[j]-r[j]) - log(z[j]-	rhat[j]))))
 }

for (i in 1:ns){                  # LOOP THROUGH STUDIES
  for (k in 1:na[i]){             # LOOP THROUGH ARMS
	
	# d[,1] is the treatment effect on the fractional polynomial intercept for NS
	# d[,2] is the treatment effect on the fractional polynomial coefficient (lumped together for NS and S)
	# d[,3] is the treatment effect on the fractional polynomial intercept for S
	#Alpha[i,k,1] <- mu[i,1] + ((d[t[i,k],1] - d[t[i,1],1]) * (1 - phist[i])) + (phist[i]* (d[t[i,k],1] - d[t[i,1],1] + hist.LHR))
	Alpha[i,k,1] <- mu[i,1] + ((d[t[i,k],1] - d[t[i,1],1]) * (1 - phist[i])) + (phist[i]* (d[t[i,k],3] - d[t[i,1],3]))
    Alpha[i,k,2] <- mu[i,2] + d[t[i,k],2] - d[t[i,1],2] # model for linear predictor of alpha_1
   }
 }
 
 totresdev <- sum(dev[])           #Total Residual Deviance

     
#priors
for (i in 1:ns){        		      # LOOP THROUGH STUDIES
  mu[i,1:2] ~ dmnorm(mean[1:2], prec[1:2,1:2])   # vague priors for all trial baselines 
 }
d[1,1]<-0        				         	# treatment effect for primary histology type is zero for reference treatment
d[1,2]<-0       					        # alpha_1 treatment effect is zero for reference treatment
d[1,3]<-0       					        # treatment effect for secondary histology type is zero for reference treatment

for (k in 2:nt){			            # LOOP THROUGH TREATMENTS
  d[k,1:2] ~ dmnorm(mean[1:2],prec[1:2,1:2])  # vague priors for treatment effects 
  
  #d[k,1] ~ dnorm(0,0.0001)
  #d[k,2] ~ dnorm(0,0.0001)
  d[k,3] <- d[k,1] + hist.LHR
 }
 
#hist.LHR ~ dnorm(0,0.0001) # Prior for HR between S and NS
hist.LHR ~ dnorm(m.hist.LHR,prec.hist.LHR) # Prior for HR between S and NS

}                                 # *** PROGRAM ENDS


